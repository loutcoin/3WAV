// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

library LibBytes {
    // solhint-disable no-inline-assembly

    // LibBytes specific errors
    error LibBytes__SliceOverflow();
    error LibBytes__SliceOutOfBounds();
    error LibBytes__AddressOutOfBounds();
    error LibBytes__HexStringException();

    bytes16 private constant SYMBOLS = "0123456789abcdef";

    function slice(
        bytes memory _bytes,
        uint256 _start,
        uint256 _length
    ) internal pure returns (bytes memory) {
        if (_length + 31 < _length) revert LibBytes__SliceOverflow();
        if (_bytes.length < _start + _length)
            revert LibBytes__SliceOutOfBounds();

        bytes memory tempBytes;

        assembly {
            switch iszero(_length)
            case 0 {
                tempBytes := mload(0x40)

                let lengthmod := and(_length, 31)

                let mc := add(
                    add(tempBytes, lengthmod),
                    mul(0x20, iszero(lengthmod))
                )
                let end := add(mc, _length)

                for {
                    let cc := add(
                        add(
                            add(_bytes, lengthmod),
                            mul(0x20, iszero(lengthmod))
                        ),
                        _start
                    )
                } lt(mc, end) {
                    mc := add(mc, 0x20)
                    cc := add(cc, 0x20)
                } {
                    mstore(mc, mload(cc))
                }
                mstore(tempBytes, _length)

                mstore(0x40, and(add(mc, 31), not(31)))
            }
            default {
                tempBytes := mload(0x40)

                mstore(tempBytes, 0)

                mstore(0x40, add(tempBytes, 0x20))
            }
        }

        return tempBytes;
    }

    function toAddress(
        bytes memory _bytes,
        uint256 _start
    ) internal pure returns (address) {
        if (_bytes.length < _start + 20) {
            revert LibBytes__AddressOutOfBounds();
        }
        address tempAddress;

        assembly {
            tempAddress := div(
                mload(add(add(_bytes, 0x20), _start)),
                0x1000000000000000000000000
            )
        }

        return tempAddress;
    }

    // Copied from LiFinance 'LibBytes.sol'; originating from OpenZeppelin's 'Strings.sol' utility library.
    function toHexString(
        uint256 value,
        uint256 length
    ) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = SYMBOLS[value & 0xf];
            value >>= 4;
        }
        if (value != 0) {
            revert LibBytes__HexStringException();
        }
        return string(buffer);
    }
}
